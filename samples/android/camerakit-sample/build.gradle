allprojects {
    ext {
        //dev>
        snapMavenUri = 'gcs://snapengine-maven-publish/releases'
        //<dev
        //partner>
        bundledMavenRepo = rootProject.file('maven')
        //<partner
        kotlinVersion = '1.6.0'
        ktxVersion = '1.7.0'
        exoPlayerVersion = '2.16.1'
        appCompatVersion = '1.4.0'
        constraintLayoutVersion = '2.1.2'
        materialVersion = '1.4.0'
        recyclerViewVersion = '1.2.1'
        drawerLayoutVersion = '1.1.1'
        glideVersion = '4.11.0'
        gmsVisionVersion = '16.2.0'

        androidMinSdkVersion = 21
        androidCompileSdkVersion = 31
        androidTargetSdkVersion = androidCompileSdkVersion

        cameraKitDistributionVersion = rootProject.file('../../../VERSION').text.trim()
        cameraKitDistributionCode = 1

        //dev>
        def buildNumber = System.getenv('BUILD_NUMBER')
        if (buildNumber && buildNumber.isInteger()) {
            cameraKitDistributionCode = buildNumber.toInteger()
        }
        cameraKitRemoteServiceGateway = System.getenv('CAMERAKIT_REMOTE_SERVICE_GATEWAY') ?: ''
        cameraKitRemoteServiceAuthority = System.getenv('CAMERAKIT_REMOTE_SERVICE_AUTHORITY') ?: ''
        cameraKitRemoteAccessToken = System.getenv('CAMERAKIT_REMOTE_ACCESS_TOKEN') ?: ''
        //<dev
        cameraKitVersion = "$cameraKitDistributionVersion+8d9694ac.167"
        // NOTE: replace the value with an ID specific to your application
        cameraKitApplicationId = 'b21cfb27-04e9-4b77-bd1a-1435522fb471'
        // NOTE: replace the value with a token specific to your application
        cameraKitApiToken = 'eyJhbGciOiJIUzI1NiIsImtpZCI6IkNhbnZhc1MyU0hNQUNQcm9kIiwidHlwIjoiSld' +
                'UIn0.eyJhdWQiOiJjYW52YXMtY2FudmFzYXBpIiwiaXNzIjoiY2FudmFzLXMyc3Rva2VuIiwibmJmIj' +
                'oxNjM4NDc0OTE0LCJzdWIiOiJiMjFjZmIyNy0wNGU5LTRiNzctYmQxYS0xNDM1NTIyZmI0NzF-U1RBR' +
                '0lOR34zMzQxMmZkZC0zMDA3LTRiMTgtOGE5OC1hNjAzZTY4MzJhMmEifQ.BBVRgyVT4I_Z_qevzAqVw' +
                'kWNXZGMHQ0s4tRJst9qfwE'
        // NOTE: replace the value with a lenses group ID specific to your application
        cameraKitLensesGroupId = '5685839489138688'
        cameraKitLensesGroupIdArCore = '5755283204734976'
    }

    buildscript {
        repositories {
            //dev>
            maven {
                url snapMavenUri
            }
            //<dev
            mavenCentral()
            google()

        }
        dependencies {
            classpath 'com.android.tools.build:gradle:4.2.2'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        }
    }

    repositories {
        //partner>
        maven {
            url bundledMavenRepo.toURI()
        }
        //<partner
        //dev>
        maven {
            url snapMavenUri
        }
        //<dev
        mavenCentral()
        google()
    }

    // AS has a bug where it incorrectly constructs a dependency version string for an artifact that
    // is published with .aar extension (Maven packaging) when executing a "download sources" task
    // that is built-in, see similar issue: https://youtrack.jetbrains.com/issue/IDEA-234036.
    // We fix it for CameraKit artifacts by removing @aar extension from version name using
    // a custom resolutionStrategy:
    configurations.all {
        if (it.name.startsWith('downloadSources')) {
            resolutionStrategy {
                eachDependency { DependencyResolveDetails details ->
                    if (details.requested.group == 'com.snap.camerakit') {
                        details.useVersion details.requested.version.replaceAll("@aar", "")
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//dev>
apply from: 'eject.gradle'
//<dev
